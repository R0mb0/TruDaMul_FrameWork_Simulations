------------------------------  ------------------------------
--        **  **        --
--  <<>>  

-- TruDaMul Framwork base implementation with base properties to check.

------------------------------ Begin Main ------------------------------
MODULE main

VAR

-- 1,2 are two variables used to distinguish the two different mules   
c  : client( m1, m2);
m1 : mule(1, c, p); 
m2 : mule(2, c, p);
p  : proxy(m1, m2, s);
s  : server(p);

--        ** Properties to Check **        --
 
-- reachability  property FALSE
--CTLSPEC
--EF(c.wallet < 0)
-- reachability  property TRUE
--CTLSPEC
--EF(m1.wallet = 50 & m2.wallet = 0)
-- liveness property FALSE
CTLSPEC
EF m1.Position = middle
CTLSPEC
AG(c.state = request -> AF(c.state = according))
CTLSPEC
AG((c.state = request & c.safety = TRUE & m1.blacklist = FALSE & m2.blacklist = FALSE) -> AF(c.state = according))
-- liveness property FALSE
CTLSPEC
AG(p.state = waitingMule -> AF(p.state = according))

------------------------------ End Main------------------------------

------------------------------ Begin Client ------------------------------
MODULE client(m1, m2)

VAR

-- state null       : No message to send.
-- state request    : Request to send a message. 
-- state according  : Arranging with the mule for the price of the mule work.
-- state waiting    : Message delivered to the mule. Wait for the answer. 
-- state response   : The mule have just deliver me the answer. 
-- state payment    : Paying the mule for his work. 

state  : {null, request, according, waiting, response, payment};

--  <<Variable Wallet contains the Tokens that the client can spends to by Mule jobs (initialized with n tokens).>>
wallet : 0..50;  

--  <<Variable Choice contains the chosen mule id. >> 
choice : 0..2;

safety : boolean;


ASSIGN

init(state)  := null;
next(state)  :=
    case
        wallet = 0 : null;
        -- The client can have a messageState to send or not.
        state = null : {null, request};
        -- At least one mule is in the client's position.
        (state = request) & (m1.Position = client | m2.Position = client) : according;
        -- Mules always accept my request. I have choose a mule.
        (safety = TRUE) & (state = according) & (choice != 0) : waiting;
        -- Mules always accept my request. I haven't choose a mule.
        (safety = TRUE) & (state = according) & (choice  = 0) : request;
        -- m1 decision
        (safety = FALSE) & (state = according) & (m1.id = choice)
          : (m1.decision = accept) ? waiting : request;
        -- m2 decision
        (safety = FALSE) & (state = according) & (m2.id = choice)
          : (m2.decision = accept) ? waiting : request; 
        -- I return in request when the messageState is lost  
        (state = waiting) & ((m1.state = goingToP & m1.messageState = lost) | 
        (m2.state = goingToP & m2.messageState = lost))  : request;    
        -- The client received the response from the server.
        (state = waiting) & (m1.state = goingToC & m1.Position = client) : response; 
        (state = waiting) & (m2.state = goingToC & m2.Position = client) : response;
        -- messageState sent to proxy.
        (state = waiting ) & ((m1.state = deliverToP) | (m2.state = deliverToP)) : payment;  
        -- response received,I pay the mule
        state  = response : payment; 
        -- Mule payed to deliver the messageState to proxy.
        (state = payment) & (m1.state = wait & m1.Position = proxy) : waiting;   
        (state = payment) & (m2.state = wait & m2.Position = proxy) : waiting;  
        -- Mule payed to delivering the answer to client.
        (state = payment) & (m1.state = wait & m2.Position = client) : null;
        (state = payment) & (m1.state = wait & m2.Position = client) : null;
        TRUE : state;
    esac;

init(wallet) := 50;
next(wallet) :=
    case
        -- Given 1 token to the mule that has completed the job.
        (wallet > 0) & (state  = payment) : wallet - 1;
        wallet = 0 : 0;
        TRUE : wallet;
    esac;

init(choice) := 0;
next(choice) :=
    case
        -- Client has to send a messageState but both mules aren't in his zone.
        (state = request) & (m1.Position != client) & (m2.Position != client) : 0;
        -- Client has to send a messageState and only m1 is in his position.
        -- m1 isn't in blacklist so client chooses him.
        (state = request) & (m1.Position  = client) & (m1.blacklist = FALSE) 
        & (m2.Position != client) : 1;
        -- Client has to send a messageState and only m1 is in his position.
        -- choice is non-deterministic.
        (state = request) & (m1.Position  = client) & (m1.blacklist = TRUE)
        & (m2.Position != client) : {0,1};       
        -- Client has to send a messageState and only m2 is in his position.
        -- m2 isn't in blacklist so client chooses him.
        (state = request) & (m1.Position != client) & (m2.Position  = client) 
        & (m2.blacklist = FALSE) : 2;
        -- Client has to send a messageState and only m2 is in his position.
        -- choice is non-deterministic.
		(state = request) & (m1.Position != client) & (m2.Position = client) 
		& (m2.blacklist = TRUE)  : {0,2};               
        -- Client has to send a messageState and both mules are in his zone.
        -- m1 isn't in blacklist. 
        (state = request) & (m1.Position  = client) & (m2.Position  = client)
        & (m1.blacklist = FALSE) & (m2.blacklist = TRUE) : 1;
        -- Client has to send a messageState and both mules are in his zone.
        -- m2 isn't in blacklist. 
        (state = request) & (m1.Position  = client) & (m2.Position  = client)
        & (m1.blacklist = TRUE) & (m2.blacklist = FALSE) : 2;
        -- Client has to send a messageState and both mules are in his zone.
        -- Both aren't in blacklist. Choice is non-deterministic. 
        (state = request) & (m1.Position  = client) & (m2.Position  = client)
        & (m1.blacklist = FALSE) & (m2.blacklist = FALSE) : {1,2};
        -- Client has to send a messageState and both mules are in his zone.
        -- Both are in blacklist. Choice is non-deterministic.
        (state = request) & (m1.Position  = client) & (m2.Position  = client)
        & (m1.blacklist = TRUE) & (m2.blacklist = TRUE) : {0,1,2};        
        -- messageState lost.
        (m1.messageState = lost) | (m2.messageState = lost) : 0;
        -- the job is done. 
        (state = payment) : 0;
        TRUE : choice;
    esac;

init(safety) := {TRUE, FALSE};
next(safety) :=
    case
    	-- when the customer pays for a job, the safety of the zone changes.
		(state = payment) : {TRUE, FALSE};
        TRUE : safety;
    esac;
    


------------------------------ End Client ------------------------------

------------------------------ Begin Mule ------------------------------

MODULE mule(id, c, p)

VAR

-- state walking     : Walking randomly waiting for a work. 
-- state talkingToC  : Arranging with the client. 
-- state talkingToP  : Arranging with the proxy.
-- state goingToP    : Travelling to deliver the messageState to proxy.
-- state goingToC    : Travelling to deliver the response to client.
-- state deliverToP  : Delivering the messageState to proxy.
-- state deliverToC  : Delivering the response to client.
-- state wait        : Waiting to get paid.

state      : {walking, talkingToC, talkingToP, goingToP, goingToC, deliverToC, deliverToP, wait};

--  <<Mule positions during the walk.>>  
Position : {client, proxy, middle};

--  <<Mule wallet (initialized with 0 tokens).>>  
wallet : 0..50;

messageState : {null, lost, owned};

blacklist : boolean;

decision : {null, accept, refuse};

ASSIGN

init(state) := walking;
next(state) :=
    case
        -- The client has a messageState to send.
        (state = walking) & (Position = client) & (c.state = request)     : talkingToC;
        -- The proxy has a response to send.
        (state = walking) & (Position = proxy)  & (p.state = waitingMule) : talkingToP;
        -- Assignment of the job to mule. 
        (state = talkingToC) & (c.choice  = id) & (decision = accept)  : goingToP;
        (state = talkingToC) & (c.choice  = id) & (decision = refuse)  : walking;
        (state = talkingToC) & (c.choice != id)  : walking;
        (state = talkingToP) & (p.choice  = id)  : goingToC;
        (state = talkingToP) & (p.choice != id)  : walking;
        -- When I lose the messageState, I go to "walking"
        (messageState = lost) : walking;
        -- Going to proxy.
        (messageState = owned) & (state = goingToP) & (Position = client) : goingToP;
        (messageState = owned) & (state = goingToP) & (Position = middle) : goingToP;
        (messageState = owned) & (state = goingToP) & (Position = proxy)  : deliverToP;
        -- Going to client.
        (messageState = owned) & (state = goingToC) & (Position = proxy)  : goingToC;
        (messageState = owned) & (state = goingToC) & (Position = middle) : goingToC;
        (messageState = owned) & (state = goingToC) & (Position = client) : deliverToC;
        -- Requesting to get paid.
        (state = deliverToP) | (state = deliverToC) : wait;
        -- Job Terminated.
        (state = wait)    : walking;
        TRUE : state;
    esac;

-- At the beginning the position is chosen non-deterministically. 
init(Position) := {client, proxy, middle};
next(Position) :=
    case
        -- Walking randomly waiting for a job.
        (state = walking)  & (Position = client) & (c.state != request) : {client, middle};
        (state = walking)  & (Position = middle) : {client, middle, proxy};
        (state = walking)  & (Position = proxy)  & (p.state != waitingMule) : {proxy, middle};
        -- When I am not chosen, I am "kicked" from the client zone.
        (state = talkingToC) & (c.choice != id) : middle;
        -- When I am not chosen, I am "kicked" from the proxy zone.
        (state = talkingToP) & (p.choice != id) : middle;       
        -- When I refuse the client, I go to middle zone.
        (state = talkingToC) & (decision = refuse) : middle;
        -- Going to proxy.
        (state = goingToP) & (Position = client) : middle;
        (state = goingToP) & (Position = middle) : proxy;
        -- Going to client.
        (state = goingToC) & (Position = proxy)  : middle;
        (state = goingToC) & (Position = middle) : client;
     
        TRUE : Position;
    esac;

init(wallet) := 0;
next(wallet) :=
    case
    	-- I can't earn anymore
    	(wallet = 50)  : 50;
        -- Earned a token for have completed the job.
        (c.state = payment) : wallet + 1;
        TRUE : wallet;
    esac;

init(messageState) := null;
next(messageState) :=
    case
    	-- when I lose the messageState, the status returns null.
    	(messageState = lost) : null;
    	-- when the mule accepts the client and he chose the mule, the messageState becomes "owned".
		(state = talkingToC) & (c.choice = id) & (decision = accept) : owned;
		-- when the mule refuses the client, the messageState remains "null".
		(state = talkingToC) & (c.choice = id) & (decision = refuse) : null;
		-- when the client refuses the mule, the messageState remains "null".
		(state = talkingToC) & (c.choice != id) : null;
		-- during safety mode, in client's zone, there is no risk of losing the messageState.
		(c.safety = TRUE)  & (state = goingToP) & (Position = client) : owned;
		-- when the client isn't in safety mode, there is the risk of losing the
		-- messageState in Client's zone.
		(c.safety = FALSE) & (state = goingToP) & (Position = client) : {lost, owned};	
		-- in "middle" zone there is always the risk of losing the messageState.
		(messageState = owned) & (state = goingToP) & (Position = middle) : {lost, owned};
		-- messageState delivered to proxy, messageState goes to "null".
		(state = deliverToP)  : null;
		-- the proxy has instructed the mule to deliver the reply.
		(state = talkingToP) & (p.choice = id) : owned;
		-- in "middle" zone there is always the risk of losing the messageState.
		(messageState = owned) & (state = goingToC) & (Position = proxy) : {lost, owned};
		-- during safety mode, in client's zone, there is no risk of losing the messageState.
        (c.safety = TRUE)  & (state = goingToC) & (Position = middle) : owned;
        -- when the client isn't in safety mode, there is the risk of losing the
		-- messageState in Client's zone.
		(c.safety = FALSE) & (state = goingToC) & (Position = middle) : {lost, owned};	
		-- response delivered to client					
		(state = deliverToC) : null;	
        TRUE : messageState;
    esac;
    
init(blacklist) := FALSE;
next(blacklist) :=
    case
		-- when i lose the messageState, i go to the blacklist.
		(messageState = lost) : TRUE;
		-- when the proxy receives the messageState or the customer receives the reply,
		-- I leave the blacklist.
		(p.state = messageState) | (c.state = response) : TRUE;
        TRUE : blacklist;
    esac;

init(decision) := null;
next(decision) :=
    case
    	-- I accept the client's request when he's in safety mode. 
    	(Position = client) & (c.state = request) & (c.safety = TRUE)  : accept;
    	-- when the client isn't in safety mode, the choice is non-deterministic 
    	(Position = client) & (c.state = request) & (c.safety = FALSE) : {refuse, accept};
    	-- conditions in which the decision returns null :
    	-- 1) messageState lost.
    	(state = goingToP) & (messageState = lost) : null;
    	-- 2) I was not chosen by the client
    	(state = talkingToC) & (c.choice != id) : null;
    	-- 3) messageState delivered
		(state = deliverToP) : null;
        TRUE : decision;
    esac;

------------------------------ End Mule ------------------------------

------------------------------ Begin Proxy ------------------------------
MODULE proxy(m1, m2, s)

VAR

-- state null        : Waiting for a message. 
-- state message     : A mule just handed a message. 
-- state forward     : Forwarding the message to the server. 
-- state wait        : Waiting to recive the server response to deliver.
-- state response    : The server just handed a response.
-- state waitingMule : Waiting for a mule to deliver the serever response.
-- state according   : Arranging with the mule to deliver the answer.

state  : {null, message, forward, wait, response, waitingMule, according};
  
--  <<Id of the mule that has been hired for the job.>>  
choice : 0..2;

ASSIGN

init(state) := null;
next(state) :=
    case
        -- Received the message.
        (state = null) & (m1.state = goingToP & m1.Position = proxy) : message;
        (state = null) & (m2.state = goingToP & m2.Position = proxy) : message;
        (state = message) : forward;
        (state = forward) : wait;
        (state = wait)    : response;
        (state = response) : waitingMule;
        -- Waiting to deliver the response to a mule.
        (state = waitingMule) & (m1.Position != proxy & m2.Position != proxy) : waitingMule;
        (state = waitingMule) & (m1.Position  = proxy | m2.Position  = proxy) : according;
        -- Returning to waiting for a message.
        (state = according) : null;  
        -- message lost, I have to send again the response.
        (m1.state = goingToC & m1.messageState = lost) : waitingMule;
        (m2.state = goingToC & m2.messageState = lost) : waitingMule;        
        TRUE : state;
    esac;

init(choice) := 0;
next(choice) :=
    case
        -- Waiting to send a response but both mules aren't in his zone.
        (state = waitingMule) & (m1.Position != proxy) & (m2.Position != proxy) : 0;
        -- Waiting to send a response to mule 1.
        (state = waitingMule) & (m1.Position  = proxy) & (m2.Position != proxy) : 1;
        -- Waiting to send a response to mule 2.
        (state = waitingMule) & (m1.Position != proxy) & (m2.Position  = proxy) : 2;
        -- Waiting to send a response but both mules are in zone. The assignment choice is non-deterministic.
        (state = waitingMule) & (m1.Position  = proxy) & (m2.Position  = proxy) : {1,2};
        -- message lost
        (m1.state = goingToC | m2.state = goingToC) & (m1.messageState = lost) | (m2.messageState = lost) : 0; 
        -- job terminated.
        (m1.state = wait) | (m2.state = wait) : 0;
        TRUE : choice;
    esac;

------------------------------ End Proxy ------------------------------

------------------------------ Begin Server ------------------------------

MODULE server(p)

VAR

-- state null     : Suspended.
-- state message  : The proxy just delivered the client message. 
-- state make     : Making the response.
-- state forward  : Forwarding the message to proxy.

state : {null, message, make , forward};

ASSIGN

init(state) := null;
next(state) :=
    case
        -- Received proxy's message.
        (state = null) & (p.state = message) : message;
        -- Making a response.
        state = message : make;
        -- Forwarding the response.
        state = make    : forward;
        -- returning to suspended state.
        state = forward : null;
        TRUE : state;
    esac;

------------------------------ End Server ------------------------------

